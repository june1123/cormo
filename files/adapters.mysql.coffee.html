<!DOCTYPE html><html lang="en"><head><meta charset="utf-8"><meta http-equiv="X-UA-Compatible" content="IE=edge"><meta name="viewport" content="width=device-width, initial-scale=1.0, maximum-scale=1.0, user-scalable=no"><title>CORMO - adapters/mysql.coffee</title><script>if (location.protocol.match(/^http/) && location.pathname.match('\.html') === null && location.pathname.slice(-1) !== '/') {
  location.href = location.href + '/';
}</script><link href="../bootstrap-3.2.0-dist/css/bootstrap.min.css" rel="stylesheet" type="text/css"><!--[if lt IE 9]><script src="https://oss.maxcdn.com/html5shiv/3.7.2/html5shiv.min.js"></script><script src="https://oss.maxcdn.com/respond/1.4.2/respond.min.js"></script><![endif]--><link href="../google-code-prettify/prettify.css" rel="stylesheet" type="text/css"><link href="../style.css" rel="stylesheet" type="text/css"></head><body data-spy="scroll" data-target=".sidebar"><nav role="navigation" class="navbar navbar-default navbar-fixed-top"><div class="navbar-header"><button type="button" data-toggle="collapse" data-target="#top-navigation-collapse" class="navbar-toggle collapsed"><span class="sr-only">Toggle navigation</span><span class="icon-bar"></span><span class="icon-bar"></span><span class="icon-bar"></span></button><div id="top-navigation-collapse" class="collapse navbar-collapse"><ul class="nav navbar-nav"><li><a href="../index.html">Home</a></li><li class="dropdown"><a data-toggle="dropdown" href="#" class="dropdown-toggle">Guides <span class="caret"></span></a><ul class="dropdown-menu"><li><a href="../guides/Aggregation.html">Aggregation</a></li><li><a href="../guides/Association.html">Association</a></li><li><a href="../guides/Callback.html">Callback</a></li><li><a href="../guides/CLI.html">Cli</a></li><li><a href="../guides/Constraint.html">Constraint</a></li><li><a href="../guides/CreateRecords.html">Create records</a></li><li><a href="../guides/DefineModels.html">Define models</a></li><li><a href="../guides/Geospatial.html">Geospatial</a></li><li><a href="../guides/Miscellaneous.html">Miscellaneous</a></li><li><a href="../guides/Query.html">Query</a></li><li><a href="../guides/Validation.html">Validation</a></li></ul></li><li><a href="../modules/index.html">Modules</a></li><li><a href="../classes/index.html">Classes</a></li><li class="dropdown active"><a data-toggle="dropdown" href="#" class="dropdown-toggle">Files - adapters/mysql.coffee <span class="caret"></span></a><ul class="dropdown-menu"><li><a href="../files/index.coffee.html">index.coffee</a></li><li><a href="../files/query.coffee.html">query.coffee</a></li><li><a href="../files/types.coffee.html">types.coffee</a></li><li><a href="../files/adapters.base.coffee.html">adapters/base.coffee</a></li><li><a href="../files/adapters.mongodb.coffee.html">adapters/mongodb.coffee</a></li><li><a href="../files/adapters.mysql.coffee.html">adapters/mysql.coffee</a></li><li><a href="../files/adapters.postgresql.coffee.html">adapters/postgresql.coffee</a></li><li><a href="../files/adapters.redis.coffee.html">adapters/redis.coffee</a></li><li><a href="../files/adapters.sql_base.coffee.html">adapters/sql_base.coffee</a></li><li><a href="../files/adapters.sqlite3.coffee.html">adapters/sqlite3.coffee</a></li><li><a href="../files/adapters.sqlite3_memory.coffee.html">adapters/sqlite3_memory.coffee</a></li><li><a href="../files/command.console.coffee.html">command/console.coffee</a></li><li><a href="../files/command.index.coffee.html">command/index.coffee</a></li><li><a href="../files/command.remote-console.coffee.html">command/remote-console.coffee</a></li><li><a href="../files/connection.association.coffee.html">connection/association.coffee</a></li><li><a href="../files/connection.index.coffee.html">connection/index.coffee</a></li><li><a href="../files/connection.manipulate.coffee.html">connection/manipulate.coffee</a></li><li><a href="../files/model.cache.coffee.html">model/cache.coffee</a></li><li><a href="../files/model.callback.coffee.html">model/callback.coffee</a></li><li><a href="../files/model.index.coffee.html">model/index.coffee</a></li><li><a href="../files/model.persistence.coffee.html">model/persistence.coffee</a></li><li><a href="../files/model.query.coffee.html">model/query.coffee</a></li><li><a href="../files/model.timestamp.coffee.html">model/timestamp.coffee</a></li><li><a href="../files/model.validate.coffee.html">model/validate.coffee</a></li><li><a href="../files/util.console.coffee.html">util/console.coffee</a></li><li><a href="../files/util.index.coffee.html">util/index.coffee</a></li><li><a href="../files/util.inflector.coffee.html">util/inflector.coffee</a></li></ul></li></ul><div class="options"><label class="checkbox"><input id="options-private" type="checkbox"> Private </label></div></div></div></nav><div class="container-fluid content"><div class="row"><div data-spy="affix" class="hidden-xs sidebar col-sm-3"><div class="cormo-sidenav"><div class="panel panel-default"><div id="undefined_body" class="panel-collapse collapse in"><ul class="nav nav-pills nav-stacked"><li><a href="../files/index.coffee.html">index.coffee</a></li><li><a href="../files/query.coffee.html">query.coffee</a></li><li><a href="../files/types.coffee.html">types.coffee</a></li></ul></div></div><div class="panel panel-default"><div data-toggle="collapse" data-target="#adapters__body" class="panel-heading">adapters/<span class="pull-right glyphicon"></span></div><div id="adapters__body" class="panel-collapse collapse in"><ul class="nav nav-pills nav-stacked"><li><a href="../files/adapters.base.coffee.html">base.coffee</a></li><li><a href="../files/adapters.mongodb.coffee.html">mongodb.coffee</a></li><li class="active"><a href="../files/adapters.mysql.coffee.html">mysql.coffee</a></li><li><a href="../files/adapters.postgresql.coffee.html">postgresql.coffee</a></li><li><a href="../files/adapters.redis.coffee.html">redis.coffee</a></li><li><a href="../files/adapters.sql_base.coffee.html">sql_base.coffee</a></li><li><a href="../files/adapters.sqlite3.coffee.html">sqlite3.coffee</a></li><li><a href="../files/adapters.sqlite3_memory.coffee.html">sqlite3_memory.coffee</a></li></ul></div></div><div class="panel panel-default"><div data-toggle="collapse" data-target="#command__body" class="panel-heading">command/<span class="pull-right glyphicon"></span></div><div id="command__body" class="panel-collapse collapse in"><ul class="nav nav-pills nav-stacked"><li><a href="../files/command.console.coffee.html">console.coffee</a></li><li><a href="../files/command.index.coffee.html">index.coffee</a></li><li><a href="../files/command.remote-console.coffee.html">remote-console.coffee</a></li></ul></div></div><div class="panel panel-default"><div data-toggle="collapse" data-target="#connection__body" class="panel-heading">connection/<span class="pull-right glyphicon"></span></div><div id="connection__body" class="panel-collapse collapse in"><ul class="nav nav-pills nav-stacked"><li><a href="../files/connection.association.coffee.html">association.coffee</a></li><li><a href="../files/connection.index.coffee.html">index.coffee</a></li><li><a href="../files/connection.manipulate.coffee.html">manipulate.coffee</a></li></ul></div></div><div class="panel panel-default"><div data-toggle="collapse" data-target="#model__body" class="panel-heading">model/<span class="pull-right glyphicon"></span></div><div id="model__body" class="panel-collapse collapse in"><ul class="nav nav-pills nav-stacked"><li><a href="../files/model.cache.coffee.html">cache.coffee</a></li><li><a href="../files/model.callback.coffee.html">callback.coffee</a></li><li><a href="../files/model.index.coffee.html">index.coffee</a></li><li><a href="../files/model.persistence.coffee.html">persistence.coffee</a></li><li><a href="../files/model.query.coffee.html">query.coffee</a></li><li><a href="../files/model.timestamp.coffee.html">timestamp.coffee</a></li><li><a href="../files/model.validate.coffee.html">validate.coffee</a></li></ul></div></div><div class="panel panel-default"><div data-toggle="collapse" data-target="#util__body" class="panel-heading">util/<span class="pull-right glyphicon"></span></div><div id="util__body" class="panel-collapse collapse in"><ul class="nav nav-pills nav-stacked"><li><a href="../files/util.console.coffee.html">console.coffee</a></li><li><a href="../files/util.index.coffee.html">index.coffee</a></li><li><a href="../files/util.inflector.coffee.html">inflector.coffee</a></li></ul></div></div></div></div><div class="col-sm-9 col-sm-offset-3"><section><h1>adapters/mysql.coffee</h1></section><pre class="prettyprint">try
  mysql = require 'mysql'
catch e
  console.log 'Install mysql module to use this adapter'
  process.exit 1

SQLAdapterBase = require './sql_base'
types = require '../types'
_ = require 'underscore'

_typeToSQL = (property) -&gt;
  if property.array
    return 'VARCHAR(255)'
  switch property.type
    when types.String then 'VARCHAR(255)'
    when types.Number then 'DOUBLE'
    when types.Boolean then 'BOOLEAN'
    when types.Integer then 'INT'
    when types.GeoPoint then 'POINT'
    when types.Date then 'DATETIME'
    when types.Object then 'VARCHAR(255)'

_propertyToSQL = (property) -&gt;
  type = _typeToSQL property
  if type
    if property.required
      type += ' NOT NULL'
    else
      type += ' NULL'
    if property.unique
      type += ' UNIQUE'
    return type

##
# Adapter for MySQL
# @namespace adapter
class MySQLAdapter extends SQLAdapterBase
  key_type: types.Integer
  support_geopoint: true
  native_integrity: true

  _convertValueType: (value, property_type) -&gt;
    if property_type is types.Date
      value = new Date value
    else if property_type is types.Number
      value = Number value
      if isNaN value
        value = Number.MAX_VALUE
    value

  ##
  # Creates a MySQL adapter
  constructor: (connection) -&gt;
    @_connection = connection

  _query: (sql, data, callback) -&gt;
    #console.log 'MySQLAdapter:', sql
    @_client.query sql, data, callback

  _createTable: (model, callback) -&gt;
    model_class = @_connection.models[model]
    tableName = model_class.tableName
    sql = []
    sql.push 'id INT NOT NULL AUTO_INCREMENT UNIQUE PRIMARY KEY'
    for column, property of model_class._schema
      column_sql = _propertyToSQL property
      if column_sql
        sql.push property._dbname + ' ' + column_sql
    for index in model_class._indexes
      columns = []
      for column, order of index.columns
        order = if order is -1 then 'DESC' else 'ASC'
        columns.push column + ' ' + order
      unique = if index.options.unique then 'UNIQUE ' else ''
      sql.push &quot;#{unique}INDEX #{index.options.name} (#{columns.join ','})&quot;
    for integrity in model_class._integrities
      if integrity.type is 'child_nullify'
        sql.push &quot;FOREIGN KEY (#{integrity.column}) REFERENCES #{integrity.parent.tableName}(id) ON DELETE SET NULL&quot;
      else if integrity.type is 'child_restrict'
        sql.push &quot;FOREIGN KEY (#{integrity.column}) REFERENCES #{integrity.parent.tableName}(id) ON DELETE RESTRICT&quot;
      else if integrity.type is 'child_delete'
        sql.push &quot;FOREIGN KEY (#{integrity.column}) REFERENCES #{integrity.parent.tableName}(id) ON DELETE CASCADE&quot;
    sql = &quot;CREATE TABLE #{tableName} ( #{sql.join ','} )&quot;
    @_query sql, (error, result) -&gt;
      return callback MySQLAdapter.wrapError 'unknown error', error if error
      callback null

  _alterTable: (model, columns, callback) -&gt;
    # TODO
    callback null

  ## @override AdapterBase::applySchema
  applySchema: (model, callback) -&gt;
    tableName = @_connection.models[model].tableName
    @_query &quot;SHOW COLUMNS FROM #{tableName}&quot;, (error, columns) =&gt;
      if error?.code is 'ER_NO_SUCH_TABLE'
        @_createTable model, callback
      else
        @_alterTable model, columns, callback

  ## @override AdapterBase::drop
  drop: (model, callback) -&gt;
    tableName = @_connection.models[model].tableName
    @_query &quot;DROP TABLE IF EXISTS #{tableName}&quot;, (error) -&gt;
      return callback MySQLAdapter.wrapError 'unknown error', error if error
      callback null

  _getModelID: (data) -&gt;
    Number data.id

  valueToModel: (value, property) -&gt;
    if property.type is types.Object or property.array
      JSON.parse value
    else if property.type is types.GeoPoint
      [value.x, value.y]
    else if property.type is types.Boolean
      value isnt 0
    else
      value

  _processSaveError = (error, callback) -&gt;
    if error.code is 'ER_NO_SUCH_TABLE'
      error = new Error('table does not exist')
    else if error.code is 'ER_DUP_ENTRY'
      key = error.message.match /for key '([^']*)'/
      error = new Error('duplicated ' + key?[1])
    else if error.code is 'ER_BAD_NULL_ERROR'
      key = error.message.match /Column '([^']*)'/
      error = new Error(&quot;'#{key?[1]}' is required&quot;)
    else
      error = MySQLAdapter.wrapError 'unknown error', error
    callback error

  _buildUpdateSetOfColumn: (property, data, values, fields, places, insert) -&gt;
    dbname = property._dbname
    value = data[dbname]
    if property.type is types.GeoPoint
      values.push value[0]
      values.push value[1]
      if insert
        fields.push dbname
        places.push 'POINT(?,?)'
      else
        fields.push dbname + '=POINT(?,?)'
    else if value?.$inc
      values.push value.$inc
      fields.push dbname + '=' + dbname + '+?'
    else
      values.push value
      if insert
        fields.push dbname
        places.push '?'
      else
        fields.push dbname + '=?'

  _buildUpdateSet: (model, data, values, insert) -&gt;
    schema = @_connection.models[model]._schema
    fields = []
    places = []
    for column, property of schema
      @_buildUpdateSetOfColumn property, data, values, fields, places, insert
    [ fields.join(','), places.join(',') ]

  _buildPartialUpdateSet: (model, data, values) -&gt;
    schema = @_connection.models[model]._schema
    fields = []
    places = []
    for column, value of data
      property = _.find schema, (item) -&gt; return item._dbname is column
      @_buildUpdateSetOfColumn property, data, values, fields, places
    [ fields.join(','), places.join(',') ]

  ## @override AdapterBase::create
  create: (model, data, callback) -&gt;
    tableName = @_connection.models[model].tableName
    values = []
    [ fields, places ] = @_buildUpdateSet model, data, values, true
    sql = &quot;INSERT INTO #{tableName} (#{fields}) VALUES (#{places})&quot;
    @_query sql, values, (error, result) -&gt;
      return _processSaveError error, callback if error
      if id = result?.insertId
        callback null, id
      else
        callback new Error 'unexpected result'

  ## @override AdapterBase::createBulk
  createBulk: (model, data, callback) -&gt;
    tableName = @_connection.models[model].tableName
    values = []
    fields = undefined
    places = []
    data.forEach (item) =&gt;
      [ fields, places_sub ] = @_buildUpdateSet model, item, values, true
      places.push '(' + places_sub + ')'
    sql = &quot;INSERT INTO #{tableName} (#{fields}) VALUES #{places.join ','}&quot;
    @_query sql, values, (error, result) -&gt;
      return _processSaveError error, callback if error
      if id = result?.insertId
        callback null, data.map (item, i) -&gt; id + i
      else
        callback new Error 'unexpected result'

  ## @override AdapterBase::update
  update: (model, data, callback) -&gt;
    tableName = @_connection.models[model].tableName
    values = []
    [ fields ] = @_buildUpdateSet model, data, values
    values.push data.id
    sql = &quot;UPDATE #{tableName} SET #{fields} WHERE id=?&quot;
    @_query sql, values, (error) -&gt;
      return _processSaveError error, callback if error
      callback null

  ## @override AdapterBase::updatePartial
  updatePartial: (model, data, conditions, options, callback) -&gt;
    tableName = @_connection.models[model].tableName
    values = []
    [ fields ] = @_buildPartialUpdateSet model, data, values
    sql = &quot;UPDATE #{tableName} SET #{fields}&quot;
    if conditions.length &gt; 0
      try
        sql += ' WHERE ' + @_buildWhere @_connection.models[model]._schema, conditions, values
      catch e
        return callback e
    @_query sql, values, (error, result) -&gt;
      return _processSaveError error, callback if error
      return callback MySQLAdapter.wrapError 'unknown error' if not result?
      callback null, result.affectedRows

  ## @override AdapterBase::findById
  findById: (model, id, options, callback) -&gt;
    select = @_buildSelect @_connection.models[model], options.select
    tableName = @_connection.models[model].tableName
    sql = &quot;SELECT #{select} FROM #{tableName} WHERE id=? LIMIT 1&quot;
    @_query sql, id, (error, result) =&gt;
      return callback MySQLAdapter.wrapError 'unknown error', error if error
      if result?.length is 1
        if options.lean
          callback null, @_refineRawInstance model, result[0], options.select, options.select_raw
        else
          callback null, @_convertToModelInstance model, result[0], options.select, options.select_raw
      else if result?.length &gt; 1
        callback new Error 'unknown error'
      else
        callback new Error 'not found'

  ## @override AdapterBase::find
  find: (model, conditions, options, callback) -&gt;
    if options.group_by or options.group_fields
      select = @_buildGroupFields options.group_by, options.group_fields
    else
      select = @_buildSelect @_connection.models[model], options.select
    if options.near? and field = Object.keys(options.near)[0]
      order_by = &quot;#{field}_distance&quot;
      location = options.near[field]
      select += &quot;,GLENGTH(LINESTRING(#{field},POINT(#{location[0]},#{location[1]}))) AS #{field}_distance&quot;
    params = []
    tableName = @_connection.models[model].tableName
    sql = &quot;SELECT #{select} FROM #{tableName}&quot;
    if conditions.length &gt; 0
      try
        sql += ' WHERE ' + @_buildWhere @_connection.models[model]._schema, conditions, params
      catch e
        return callback e
    if options.group_by
      sql += ' GROUP BY ' + options.group_by.join ','
    if options.conditions_of_group.length &gt; 0
      try
        sql += ' HAVING ' + @_buildWhere options.group_fields, options.conditions_of_group, params
      catch e
        return callback e
    if options?.orders.length &gt; 0 or order_by
      orders = options.orders.map (order) -&gt;
        if order[0] is '-'
          return order[1..] + ' DESC'
        else
          return order + ' ASC'
      if order_by
        orders.push order_by
      sql += ' ORDER BY ' + orders.join ','
    if options?.limit?
      sql += ' LIMIT ' + options.limit
      sql += ' OFFSET ' + options.skip if options?.skip?
    else if options?.skip?
      sql += ' LIMIT 2147483647 OFFSET ' + options.skip
    #console.log sql, params
    @_query sql, params, (error, result) =&gt;
      #console.log result
      return callback MySQLAdapter.wrapError 'unknown error', error if error
      if options.group_fields
        callback null, result.map (record) =&gt; @_convertToGroupInstance model, record, options.group_by, options.group_fields
      else
        if options.lean
          callback null, result.map (record) =&gt; @_refineRawInstance model, record, options.select, options.select_raw
        else
          callback null, result.map (record) =&gt; @_convertToModelInstance model, record, options.select, options.select_raw

  ## @override AdapterBase::count
  count: (model, conditions, callback) -&gt;
    params = []
    tableName = @_connection.models[model].tableName
    sql = &quot;SELECT COUNT(*) AS count FROM #{tableName}&quot;
    if conditions.length &gt; 0
      try
        sql += ' WHERE ' + @_buildWhere @_connection.models[model]._schema, conditions, params
      catch e
        return callback e
    #console.log sql, params
    @_query sql, params, (error, result) =&gt;
      return callback MySQLAdapter.wrapError 'unknown error', error if error
      return callback error 'unknown error' if result?.length isnt 1
      callback null, Number(result[0].count)

  ## @override AdapterBase::delete
  delete: (model, conditions, callback) -&gt;
    params = []
    tableName = @_connection.models[model].tableName
    sql = &quot;DELETE FROM #{tableName}&quot;
    if conditions.length &gt; 0
      try
        sql += ' WHERE ' + @_buildWhere @_connection.models[model]._schema, conditions, params
      catch e
        return callback e
    #console.log sql, params
    @_query sql, params, (error, result) -&gt;
      return callback new Error 'rejected' if error and error.code in ['ER_ROW_IS_REFERENCED_', 'ER_ROW_IS_REFERENCED_2']
      return callback MySQLAdapter.wrapError 'unknown error', error if error or not result?
      callback null, result.affectedRows

  ##
  # Connects to the database
  # @param {Object} settings
  # @param {String} [settings.host]
  # @param {Number} [settings.port]
  # @param {String} [settings.user]
  # @param {String} [settings.password]
  # @param {String} settings.database
  # @nodejscallback
  connect: (settings, callback) -&gt;
    # connect
    client = mysql.createConnection
      host: settings.host
      port: settings.port
      user: settings.user
      password: settings.password
    client.connect (error) =&gt;
      return callback MySQLAdapter.wrapError 'failed to connect', error if error

      @_client = client

      @_selectDatabase settings, callback

  _selectDatabase: (settings, callback) -&gt;
    # select database
    @_client.query &quot;USE `#{settings.database}`&quot;, (error) =&gt;
      return callback null if not error

      # create one if not exist
      if error.code is 'ER_BAD_DB_ERROR'
        @_client.query &quot;CREATE DATABASE `#{settings.database}`&quot;, (error) =&gt;
          return callback MySQLAdapter.wrapError 'unknown error', error if error
          @_selectDatabase settings, callback
      else
        msg = if error.code is 'ER_DBACCESS_DENIED_ERROR' then &quot;no access right to the database '#{settings.database}'&quot; else 'unknown error'
        callback MySQLAdapter.wrapError msg, error

  ## @override AdapterBase::close
  close: -&gt;
    if @_client
      @_client.end()
    @_client = null

module.exports = (connection) -&gt;
  new MySQLAdapter connection</pre></div></div></div><script src="http://code.jquery.com/jquery-1.11.0.min.js"></script><script src="../bootstrap-3.2.0-dist/js/bootstrap.min.js"></script><script src="../google-code-prettify/prettify.js"></script><script src="../script.js"></script><script src="../group-examples.js"></script><a href="https://github.com/croquiscom/cormo"><img src="https://s3.amazonaws.com/github/ribbons/forkme_right_green_007200.png" alt="Fork me on GitHub" class="github-ribbon"></a></body></html>